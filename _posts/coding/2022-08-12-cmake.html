---
layout: post
category: coding
title: The right way to link with Gurobi using Modern CMake
short: Let's get this done once and for all. (Included, a ready-to-use FindGUROBI.cmake file)
date: 2022-08-12
preview: cmake.png
---

<p>
Stop insulting your PC, you are in the right place. I know... Not being able to link with a given library is among the most awful feelings in computer programming. Fortunately, we are here to discuss this and to show how to link, in a proper way, with a C++ "library" called <a href="https://www.gurobi.com/">Gurobi</a>.
</p>

<p>
    <b>For a ready-to-use FindGUROBI.cmake, please visit my <a href="https://github.com/hlefebvr/FindGUROBI.cmake">FindGUROBI.cmake</a> repo</b>.
</p>

<p>
Gurobi is one of the most efficient mathematical optimization solver that exist in the world and is used by tons of people. Strangely enough, I have only seen little help on how to link with it using CMake. Even worse, the only examples I could found (e.g., searching for <code>FindGUROBI.cmake</code> on GitHub) are simply... wrong. What I mean by wrong is a little bit subjective, I have to admit. But I say this in the sense that, even though the examples you find may work, none of them are safe and none of them use the <a href="https://gist.github.com/mbinna/c61dbb39bca0e4fb7d1f73b0d66a4fd1">Modern CMake</a> paradigm. This is, in particular true for the official solution given by Gurobi itself!
</p>

<p>
For those of you who do not know what "Modern CMake" is. You can consider it as just a bunch of principles to be used when writting CMake files so that it is clean, elegant and powerful. It requires features that have been introduced in CMake versions 3.0.0 and more. The most important principles are as follows:
    <ul>
        <li>Treat CMake code like production code ;</li>
        <li>Forget about <code>add_compile_options</code>, <code>include_directories</code>, <code>link_directories</code>, <code>link_libraries</code> ;</li>
        <li>Be "target-oriented" instead.</li>
    </ul>
</p>

<p>
For more details on this, you may refer to <a href="https://gist.github.com/mbinna/c61dbb39bca0e4fb7d1f73b0d66a4fd1">the gist of 
mbinna</a> or to the book <a href="https://www.packtpub.com/product/Modern-CMake-for-C/9781801070058">Modern CMake for C++</a>. To ease your task, I will start by showing you what we do not want (the ugly old-style CMake). Then, we will work on a better version. But, before that, we need to talk about how CMake finds new libraries in your system. Basically, what does <code>find_package</code> do?
</p>

<h2>How does CMake finds packages</h2>

<p>
All right, let's start with a new CMake project. I will assume that you already have a valid <code>main.cpp</code>. Let us start with the following standard <code>CMakeLists.cmake</code>.
<pre><code class="cmake">cmake_minimum_required(VERSION 3.19)
project(my_project)

add_executable(my_target main.cpp)
</code></pre>
</p>

<p>
    What this code does is rather simple. First, the <code>cmake_minimum_required</code> command is called to specify CMake version requirements. I am using version <code>3.19</code> in this case (probably lower versions will work too). Then, a new project (i.e., a set of targets) is created with <code>project</code>. Then, a new target called <code>my_target</code> is created. This target will be an executable since the <code>add_executable</code> command is being used. Following the name <code>my_target</code> of the target, we have the list of source files which will be compiled for building the executable. 
</p>

<p>
    Note that if you do not know what a target is, you can simply view it as a "CMake object" with correspoding attributes and methods. For instance, you may use <code>set_target_properties</code> to set your target's properties. Modern CMake asks for a target-oriented viewpoint while writting CMake files. 
</p>

<p>
    OK, now let's search for our package! This is done by using the <code>find_package</code> command.
</p>

<pre><code class="cmake">find_package(GUROBI REQUIRED)</code></pre>

<p>
    The first parameter is the name of the package you are looking for, here, it is <code>GUROBI</code>. Then, <code>REQUIRED</code> tells CMake that the generation should stop if <code>GUROBI</code> is not found, since it is <code>REQUIRED</code>. Now let us try to run <code>cmake</code> on this. (Easy steps are to create a new folder <code>mkdir build</code> then to run cmake inside of it as follows: <code>cd build && cmake ..</code>). We get the following error message.
</p>

<pre><code class="shell">CMake Error at CMakeLists.txt:14 (find_package):
  By not providing "FindGUROBI.cmake" in CMAKE_MODULE_PATH this project has
  asked CMake to find a package configuration file provided by "GUROBI", but
  CMake did not find one.

  Could not find a package configuration file provided by "GUROBI" with any
  of the following names:

    GUROBIConfig.cmake
    gurobi-config.cmake

  Add the installation prefix of "GUROBI" to CMAKE_PREFIX_PATH or set
  "GUROBI_DIR" to a directory containing one of the above files.  If "GUROBI"
  provides a separate development package or SDK, be sure it has been
  installed.
</code></pre>

<p>
    Let us try to understand this message. First, CMake tells us that it was looking for a file called <code>FindGUROBI.cmake</code> but was unable to find it. It was looking for it in the <code>CMAKE_MODULE_PATH</code> directories. Note that you can print the list of paths by executing <code>message("${CMAKE_MODULE_PATH}")</code>. According to the <a href="https://cmake.org/cmake/help/v3.0/command/find_package.html#:~:text=CMake%20searches%20for%20a%20file,and%20producing%20any%20needed%20messages.">documentation</a>, this corresponds to the "Module" mode of <code>find_package</code>. Simply put, CMake searches for a file called <code>FindGUROBI.cmake</code>. If it cannot be found, an error is returned. If it is found, the file is executed and no error is returned. The library has been "found". From CMake's viewpoint, the hard part is to locate the file <code>FindGUROBI.cmake</code> since this file is supposed to do all the work to actually provide informations on how to link with <code>GUROBI</code>.
</p>

<p>
    Then, CMake switches to the "Config" mode (only when the "Module" mode failed). In this mode, CMake searches for files named <code>GUROBIConfig.cmake</code> or <code>gurobi-config.cmake</code> in the paths listed in <code>CMAKE_PREFIX_PATH</code> or in the <code>GUROBI_DIR</code>. In principle, these files should have been provided by the package developpers during the installation process. If this were the case, a simple execution of <code>find_package(GUROBI REQUIRED)</code> would have worked and be enough to "find" the package!  Unfortunately, Gurobi does not provide such a file. 
</p>

<p>
    Thus, we will have to "manually" tell CMake how to find <code>GUROBI</code>. Simply enough, we will do it by writing our own <code>FindGUROBI.cmake</code> file. Create a new folder called <code>cmake</code> with <code>mkdir cmake</code> and create a new file <code>cmake/FindGUROBI.cmake</code>. This is the file which we will be executed by CMake when calling <code>find_package</code>. Now, remember that CMake only looks for <code>FindGUROBI.cmake</code> in the list of directories which are in the <code>CMAKE_MODULE_PATH</code> variable. Thus, let us add <code>cmake/</code> as a potential location folder. This is done as follows.
</p>

<pre><code class="cmake">set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake")</code></pre>

<p>
    From now on, CMake should be able to find our file. Remember that CMake considers that finding the <code>FindGUROBI.cmake</code> file and executing it is enough to "actually" find the package. In our case, clearly, the <code>FindGUROBI.cmake</code> file is empty and does nothing. We will have to write it! Before diving into it, I would like to give you an example of what we do not want as a result. That is, the old-style-CMake way of doing things.
</p>

<h2>The Ugly old-style CMake - What we do not want</h2>

<p>
    When searching the internet of examples <code>FindGUROBI.cmake</code> I have found a lot of files which could be used as follows.
</p>

<pre><code class="cmake">find_package(GUROBI REQUIRED)

add_executable(my_target main.cpp)
include_directories(${GUROBI_INCLUDE_DIRS})
link_libraries(${GUROBI_LIBRARY})
</code></pre>

<p>
    This type of code poses several issues. First, it relies on "global variables" which have been created by the execution of <code>FindGUROBI.cmake</code>. These variables are <code>GUROBI_INCLUDE_DIRS</code> and <code>GUROBI_LIBRARY</code>, for instance. These variables are globally accessible and non-const, meaning that they can be altered by any piece of CMake code by calling <code>set(GUROBI_LIBRARY "my junk value")</code>. What's more, to make these variables "globally accessible", the <code>FindGUROBI.cmake</code> (and therefore those who write this file) have to use the <code>mark_as_advanced</code> command. Just by the name, you can tell you should not be using it.
</p>

<p>
    Another issue is the use of <code>include_directories</code> and <code>link_libraries</code> which are non-target-based commands. In that sense, any given target will be linked with Gurobi and all targets will have access to the include directories. This is both unncessary and inelegant. Moreover, there is no control on how to propagate these dependencies. Consider creating a library which depends on other libraries, including Gurobi. How to tell CMake that a piece of code which links to your library should also link with Gurobi and other dependencies, and which dependencies should not be linked with? 
</p>

<p>
    I think you get my point, let's write our own Modern CMake <code>FindGUROBI.cmake</code> file!
</p>

<h2>Modern CMake</h2>

<p>
OK, this part is dedicated to the actual writing of our <code>FindGUROBI.cmake</code> file. At the end of this section, you will be able to write the following CMake code.
</p>
<pre><code class="cmake">find_package(GUROBI REQUIRED)
add_executable(my_target main.cpp)
target_link_libraries(my_target PUBLIC gurobi)
</code></pre>

<p>
    This piece of code is beautiful. And I am not (only) saying this because it is mine, but because it is target-oriented, global-variable-free and controls dependency propagation. It is easily read (only one call to <code>target_link_libraries</code>) and, even better, does not need an underlying <code>FindGUROBI.cmake</code> which make use of any strange behaviour such as <code>mark_as_advanced</code> commands. Let's start!
</p>

<p>
At this point, I will assume that Gurobi is installed in your machine, following <a href="https://www.gurobi.com/documentation/9.5/quickstart_linux/software_installation_guid.html">these official guidelines</a> or that the corresponding variables (<code>GUROBI_HOME</code>, etc.) have been set accordingly. Once this is done, we can start writting our <code>FindGUROBI.cmake</code> file! Roughly speakking, we need to do the following steps:
    <ul>
        <li>Find the library files; </li>
        <li>Find the include directory;</li>
        <li>Check that everything has been found;</li>
        <li>Create a target named <code>gurobi</code> and set its properties;</li>
    </ul>
</p>

<h3>Finding the library files</h3>

<p>
According to <a href="https://www.gurobi.com/documentation/9.5/quickstart_linux/cpp_building_and_running_t.html">this official Gurobi page</a>, for our C++ code to work with Gurobi we need to link with two different libraries: the Gurobi C++ library <code>libgurobi_c++.a</code> <b>and</b> the Gurobi C library <code>libgurobi95.so</code>. Thus, we will have to look for both of these files. Let us start with <code>libgurobi95.so</code>. This is done as follows.
</p>

<pre><code class="cmake">find_library(
        GUROBI_LIBRARY
        NAMES gurobi gurobi81 gurobi90 gurobi95 gurobi100
        HINTS ${GUROBI_DIR} $ENV{GUROBI_HOME}
        PATH_SUFFIXES lib)
</code></pre>

<p>
    Some explanations on this command is needed. First, <code>find_library</code> is broadly used to find the path of library files, e.g., </code>.so</code> or </code>.a</code> files, and stores the result in a cache variable. In our case, this variable's name is <code>GUROBI_LIBRARY</code>. Then, we give some indications on the possible names of the file which we are looking for with the <code>NAMES</code> keyword. Thus, we are telling CMake to search for a file with possible names <code>gurobi</code>, <code>gurobi81</code>, <code>gurobi90</code> or <code>gurobi95</code>. Then, we also need to provide some "hints" on where to find this file by using the <code>HINTS</code> keyword. In our case, we are going to have a look to the directories path stored in the cache variable <code>GUROBI_DIR</code> or the path inside the environment variable <code>GUROBI_HOME</code>. If you installed Gurobi following the official guidelines, an appropriate environment variable should exists. Then, to each of these paths, we add the "lib" suffix by using the <code>PATH_SUFFIXES</code> keyword. Indeed, by default, we have the environment variable <code>GUROBI_HOME=/opt/gurobi951/linux64</code> (or alike - again, following the official installation guidelines). Yet, the library file we are looking for is located inside <code>/opt/gurobi951/linux64/lib</code>. At this point, we should be able to run <code>message("${GUROBI_LIBRARY}")</code>. This should print out something like <code>/opt/gurobi951/linux64/lib/libgurobi95.so</code>.
</p>

<p>
OK, now we need to do the same for the C++ library <code>libgurobi_c++.a</code>. The steps are very similar. For non-Visual-Studio-Code users, the following command will do the trick.
</p>

<pre><code class="cmake">find_library(
        GUROBI_CXX_LIBRARY
        NAMES gurobi_c++
        HINTS ${GUROBI_DIR} $ENV{GUROBI_HOME}
        PATH_SUFFIXES lib)
</code></pre>

<p>
    Again, we are looking for a library file named <code>gurobi_c++</code> inside the folders with path contained in the cache variable <code>GUROBI_DIR</code> or in the environment variable <code>GUROBI_HOME</code> with a suffix <code>lib</code>. For users of Visual Studio Code, I am relying on this <a href="https://support.gurobi.com/hc/en-us/articles/360039499751-How-do-I-use-CMake-to-build-Gurobi-C-C-projects-">"official Gurobi FindGUROBI.cmake file"</a> which, as I anticipated, does not fulfill the Modern CMake standards and has the previously discussed drawbacks. All in all, we get the following code to search for <code>libgurobi_c++.a</code>.
</p>

<pre><code class="cmake">if(MSVC)
    # determine Visual Studio year
    if(MSVC_TOOLSET_VERSION EQUAL 142)
        set(MSVC_YEAR "2019")
    elseif(MSVC_TOOLSET_VERSION EQUAL 141)
        set(MSVC_YEAR "2017")
    elseif(MSVC_TOOLSET_VERSION EQUAL 140)
        set(MSVC_YEAR "2015")
    endif()

    if(MT)
        set(M_FLAG "mt")
    else()
        set(M_FLAG "md")
    endif()

    find_library(
            GUROBI_CXX_LIBRARY
            NAMES gurobi_c++${M_FLAG}${MSVC_YEAR}
            HINTS ${GUROBI_DIR} $ENV{GUROBI_HOME}
            PATH_SUFFIXES lib)
else()
    find_library(
            GUROBI_CXX_LIBRARY
            NAMES gurobi_c++
            HINTS ${GUROBI_DIR} $ENV{GUROBI_HOME}
            PATH_SUFFIXES lib)
endif()
</code></pre>

<h3>Finding the include directory</h3>

<p>
    Quite similarly, we search for the include directories by using the <code>find_path</code> command, which works much similarly to <code>find_library</code>. Have a look at the self-explained piece of code.
</p>

<pre><code class="cmake">find_path(
        GUROBI_INCLUDE_DIRS
        NAMES gurobi_c.h
        HINTS ${GUROBI_DIR} $ENV{GUROBI_HOME}
        PATH_SUFFIXES include)
</code></pre>

<h3>Checking that everything has been found</h3>

<p>
    We are now almost ready to create our <code>gurobi</code> target with which our own target should link. Yet, we first need to check that everything has been found. Among other things, this can be done thanks to the <code>find_package_handle_standard_args</code> command, which is defined in the <code>FindPackageHandleStandardArgs</code> module. It is used as follows.
</p>

<pre><code class="cmake">include(FindPackageHandleStandardArgs) # include the "FindPackageHandleStandardArgs" module
find_package_handle_standard_args(GUROBI DEFAULT_MSG GUROBI_LIBRARY GUROBI_CXX_LIBRARY GUROBI_INCLUDE_DIRS)
</code></pre>

<p>
    The <code>find_package_handle_standard_args</code> command will have two main effects. First, it will check that variables <code>GUROBI_LIBRARY</code>, <code>GUROBI_CXX_LIBRARY</code> and <code>GUROBI_INCLUDE_DIRS</code> have well been given a value. In other words, checks that we have found every path we needed. If this is the case, a variable <code>GUROBI_FOUND</code> is created and set to true. Then, its second effect is to take into account the <code>REQUIRED</code> or <code>QUIET</code> arguments of the <code>find_package</code> command. For instance, when we run <code>find_package(gurobi REQUIRED)</code>, cmake will stop if the library cannot be found. All is clear, we are now ready to create our target!
</p>

<h3>Creating the gurobi target</h3>

<p>
    If every path is found, we are good for creating our target. This target will be a "library" target, rather than an "executable" target. Thus, we will be using the <code>add_library</code> command, instead of the <code>add_executable</code> target. The C++ Gurobi library is a static library (its extension is </code>.a</code>) and we are going to "import" it. Thus, we do the following.
</p>

<pre><code class="cmake">add_library(gurobi STATIC IMPORTED)
set_target_properties(gurobi PROPERTIES IMPORTED_LOCATION ${GUROBI_CXX_LIBRARY})
</code></pre>

<p>
    Quite simply, we first create an imported static library as a target named <code>gurobi</code>, then set its <code>IMPORTED_LOCATION</code> property to <code>${GUROBI_CXX_LIBRARY}</code>. Now, recall that any C++ program which is intended to work with gurobi should alos be linked with the C Gurobi library <code>libgurobi95.so</code>. We will therefore use the "dependency propagation" feature of CMake. First, we will link our gurobi target to the C library by using <code>target_link_libraries</code> command with the <code>INTERFACE</code> keyword. By choosing the <code>INTERFACE</code> keyword, CMake will automatically propagate this "linkage requirement" to any target linking with the <code>gurobi</code> imported target itself. The same is done for the include directories which we may add by using the <code>target_include_directories</code> command. This is done as follows.
</p>

<pre><code class="cmake">if (GUROBI_FOUND)
    add_library(gurobi STATIC IMPORTED)
    set_target_properties(gurobi PROPERTIES IMPORTED_LOCATION ${GUROBI_CXX_LIBRARY})
    target_link_libraries(gurobi INTERFACE ${GUROBI_LIBRARY})
    target_include_directories(gurobi INTERFACE ${GUROBI_INCLUDE_DIRS})
endif()
</code></pre>

<p>
    That's it! We are now done with writting our <code>FindGUROBI.cmake</code> file.
</p>

<h2>FAQ: How do I resolve "undefined reference" errors while linking Gurobi in C++?</h2>

<p>
    This is a common issue. All you need to do is to re-compile the Gurobi library. Please, refer to this <a href="https://support.gurobi.com/hc/en-us/articles/360039093112">Official post from Gurobi</a>.
</p>